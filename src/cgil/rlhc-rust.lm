include 'ril.lm'

namespace rust_out
	token _IN_ /''/
	token _EX_ /''/

	lex
		token comment /
			'//' any* :> '\n' |
			'/*' any* :>> '*/'
		/

		token id
			/[a-zA-Z_][a-zA-Z_0-9]*/

		token number /
			[0-9]+
		/

		token symbol /
			'!' | '#' | '$' | '%' | '&' | '(' | ')' | '*' |
			'+' | ',' | '-' | '.' | '/' | ':' | ';' | '<' |
			'=' | '>' | '?' | '@' | '[' | ']' | '^' | '|' |
			'~' /

		literal `{ `}

		token lifetime / "'" id /

		token string /
				'"' ( [^"\\] | '\\' any ) * '"' |
				"'" ( [^\\] | '\\' any ) "'"
			/

		ignore
			/[ \t\v\r\n]+/
	end

	def item
		[comment]
	|	[id]
	|	[number]
	|	[symbol]
	|	[string]
	|	[lifetime]
	|	[`{ _IN_ item* _EX_ `} ]

	def rust_out
		[_IN_ _EX_ item*]
end

namespace rust_gen

	global Parser: parser<rust_out::rust_out>

	void tok_list( TL: host::tok* )
	{
		for Tok: host::tok in repeat(TL) {
			switch Tok
			case [host::`${ StmtList: stmt* host::`}$]
				send Parser "{[stmt_list( StmtList )]}"
			case [host::`={ Expr: expr host::`}=]
				send Parser "([expr( Expr )])"
			case [E: escape] {
				Str: str = $E
				send Parser
					"[Str.suffix( 1 )]"
			}
			default
				send Parser [Tok]
		}
	}

	void embedded_host( EmbeddedHost: embedded_host )
	{
		switch EmbeddedHost
		case [`host `( string `, uint `) `={ TL: host::tok* host::`}=]
		{
			send Parser
				"([tok_list( TL )])"
		}
		case [`host `( string `, uint `) `${ TL: host::tok* host::`}$]
		{
			send Parser 
				"{[tok_list( TL )]}
		}
		case [`host `( string `, uint `) `@{ TL: host::tok* host::`}@]
		{
			send Parser
				[tok_list( TL )]
		}
	}

	void expr_factor( ExprFactor: expr_factor )
	{
		switch ExprFactor
		case [EH: embedded_host]
		{
			send Parser
				[embedded_host( EH )]
		}
		case [O:`( TL: expr C: `)]
		{
			send Parser
				[O expr(TL) C]
		}
		case [EF: expr_factor `[ E: expr `]]
		{
			send Parser
				"[expr_factor(EF)]\[([expr( E )]) as usize\]"
		}
		case ['offset' '(' E1: expr ',' E2: expr ')']
		{
			send Parser
				"( [expr( E2 )] ) as i32"
		}
		case ['deref' '(' E1: expr ',' E2: expr ')']
		{
			send Parser
				"[expr( E1 )]\[([expr( E2 )]) as usize\]
		}
		case [T: `TRUE]
		{
			T.data = 'true'
			send Parser [T]
		}
		case [F: `FALSE]
		{
			F.data = 'false'
			send Parser [F]
		}
		case [N: `nil]
		{
			N.data = '0'
			send Parser [N]
		}
		case [Number: number]
		{
			number( Number )
		}
		case [E1: embedded_host `-> E2: expr_factor]
		{
			# The accessor operator is contained wihtin the lhs.
			embedded_host( E1 )
			expr_factor( E2 )
		}
		case [`cast `( T: type `) E: expr_factor]
		{
			send Parser
				"( [expr_factor( E )] ) as [type(T)]"
		}
		case [I: ident `[ E: expr `] `. F: ident] {
			send Parser
				[^I '_' ^F '[' expr(E) ' as usize]']
		}
		default {
			# Catches cases not specified
			send Parser [ExprFactor]
		}
	}

	void lvalue( ExprFactor: lvalue )
	{
		switch ExprFactor
		case [EH: embedded_host]
		{
			send Parser
				[embedded_host( EH )]
		}
		case [LV: lvalue `[ E: expr `]]
		{
			send Parser
				"[lvalue( LV )]\[([expr( E )]) as usize\]
		}
		case [E1: embedded_host `-> E2: lvalue]
		{
			# The accessor operator is contained wihtin the lhs.
			embedded_host( E1 )
			lvalue( E2 )
		}
		case [I: ident `[ E: expr `] `. F: ident] {
			send Parser
				[^I '_' ^F '[' expr(E) ' as usize]']
		}
		default {
			# Catches cases not specified
			send Parser [ExprFactor]
		}
	}

	void expr_factor_op( ExprFactorOp: expr_factor_op )
	{
		switch ExprFactorOp
		case [B: `! expr_factor_op]
		{
			send Parser [B]
			expr_factor_op( ExprFactorOp._expr_factor_op )
		}
		case [`~ EFO: expr_factor_op]
		{
			send Parser
				"![expr_factor_op( EFO )]
		}
		case [expr_factor]
		{
			expr_factor( ExprFactorOp.expr_factor )
		}
	}

	void expr_bitwise( ExprBitwise: expr_bitwise )
	{
		switch ExprBitwise
		case [expr_bitwise A: `& expr_factor_op]
		{
			expr_bitwise( ExprBitwise._expr_bitwise )
			send Parser [A]
			expr_factor_op( ExprBitwise.expr_factor_op )
		}
		case [expr_factor_op]
		{
			expr_factor_op( ExprBitwise.expr_factor_op )
		}
	}

	void expr_mult( ExprMult: expr_mult )
	{
		switch ExprMult
		case [expr_mult T: `* expr_bitwise]
		{
			expr_mult( ExprMult._expr_mult )
			send Parser [T]
			expr_bitwise( ExprMult.expr_bitwise )
		}
		case [expr_bitwise]
		{
			expr_bitwise( ExprMult.expr_bitwise )
		}
	}

	void expr_add( ExprAdd: expr_add )
	{
		switch ExprAdd
		case [expr_add Op: add_op expr_mult]
		{
			expr_add( ExprAdd._expr_add )
			send Parser [Op]
			expr_mult( ExprAdd.expr_mult )
		}
		case [expr_mult]
		{
			expr_mult( ExprAdd.expr_mult )
		}
	}

	void expr_shift( ExprShift: expr_shift )
	{
		switch ExprShift
		case [expr_shift Op: shift_op expr_add]
		{
			expr_shift( ExprShift._expr_shift )
			send Parser [Op]
			expr_add( ExprShift.expr_add )
		}
		case [expr_add]
		{
			expr_add( ExprShift.expr_add )
		}
	}

	void expr_test( ExprTest: expr_test )
	{
		switch ExprTest
		case [expr_test Op: test_op expr_shift]
		{
			expr_test( ExprTest._expr_test )
			send Parser [Op]
			expr_shift( ExprTest.expr_shift )
		}
		case [expr_shift]
		{
			expr_shift( ExprTest.expr_shift )
		}
	}

	void expr( Expr: expr )
	{
		expr_test( Expr.expr_test )
	}

	void type( Type: type )
	{
		switch Type
		case "s8"
			send Parser ['i8 ']
		case "s16"
			send Parser ['i16 ']
		case "s32"
			send Parser ['i32 ']
		case "s64"
			send Parser ['i64 ']
		case "s128"
			send Parser ['i128']
		case "int"
			send Parser ['i32']
		case "uint"
			send Parser ['u32']
		default
			send Parser [Type]
	}

	void number( Number: number )
	{
		switch Number
		case [`u `( uint `) ]
			send Parser "[Number.uint]"
		default
			send Parser [Number]
	}

	void num_list( NumList: num_list )
	{
		for Number: number in NumList
			send Parser "[number( Number )], "
		send Parser "0"
	}

	void stmt( Stmt: stmt )
	{
		switch Stmt
		case [EH: embedded_host]
		{
			send Parser
				[embedded_host( EH )]
		}
		case [A: static_array] {
			Length: int = 1
			for Number: number in A.num_list
				Length = Length + 1

			send Parser
				"static [A.ident]: \[[type(A.type)]; [Length]\] = \[ [num_list(A.num_list)] \];
		}
		case [V: static_value] {
			send Parser
				"static [V.ident]: i32 = [V.number];
		}
		case [D: declaration] {
			send Parser
				"let mut [D.ident] "

			switch D.opt_init
			case [E: `= expr] {
				send Parser
					"= [expr(D.opt_init.expr)];
			}
			default {
				send Parser
					"= 0;
			}
		}
		case [Index: index_stmt]
		{
			send Parser
				"let mut [Index.ident] :i32"

			switch Index.opt_init
			case [E: `= expr] {
				send Parser
					"= [expr(Index.opt_init.expr)];
			}
			default {
				send Parser
					"= 0;
			}
		}
		case [
				'if' `( IfExpr: expr `) IfStmt: stmt
				ElseIfClauseList: else_if_clause* ElseClauseOpt: else_clause?
		] {
			send Parser
				"if ( [expr(IfExpr)] ) {
				"	[stmt(IfStmt)]
				"}

			for ElseIfClause: else_if_clause in repeat( ElseIfClauseList ) {
				match ElseIfClause
					['else if (' ElseIfExpr: expr ')' ElseIfStmt: stmt]

				send Parser
					"else if ( [expr(ElseIfExpr)] ) {
					"	[stmt(ElseIfStmt)]
					"}
			}

			if ( match ElseClauseOpt ['else' ElseStmt: stmt] ) {
				send Parser
					"else {
					"	[stmt(ElseStmt)]
					"}
			}
		}
		case [`continue CL: ident `;] {
			send Parser
				"continue '[CL];
		}
		case [`break BL: ident `;] {
			send Parser
				"break '[BL];
		}
		case [BL: break_label? 'while' '(' WhileExpr: expr ')' '{' StmtList: stmt* '}' ] {
			if match BL [bl: break_label]
				send Parser "'[bl.ident]: "
				
			send Parser
				"while ( [expr(WhileExpr)] ) {
				"	[stmt_list(StmtList)]
				"}
		}
		case [`switch `( SwitchExpr: expr `) `{ StmtList: stmt* `}] {
			send Parser
				"match ( [expr(SwitchExpr)] ) {
				"	[stmt_list(StmtList)]

			NeedDef: bool = true
			for Stmt: stmt in repeat(StmtList) {
				if match Stmt [default_block]
					NeedDef = false
			}

			if NeedDef {
				send Parser
					"	_ => {}
			}

			send Parser
				"}
		}
		case [ExprExpr: expr Semi: `;] {
			send Parser
				[expr(ExprExpr) Semi]
		}
		case [L: `{ TL: stmt* R: `}] {
			send Parser
				[L stmt_list(TL) R]
		}
		case [
			TypeList: opt_const Type: type
			Ident: ident OptInit: opt_init Semi: `;
		]
		{
			send Parser
				[TypeList type(Type) Ident]

			if match OptInit [E: `= expr] {
				send Parser
					[E expr(OptInit.expr)]
			}

			send Parser
				[Semi]
		}
		case [Export: export_stmt]
		{
			send Parser
				"#define [Export.ident] [number(Export.number)] 
		}
		case ['fallthrough' ';']
		{
			# Nothing needed here.
		}
		case [case_block]
		{
			send Parser
				"[expr( Stmt.case_block.expr )] => {
				"[stmt_list( Stmt.case_block._repeat_stmt )]
				"}
		}
		case [default_block]
		{
			send Parser
				"_ => {
				"[stmt_list( Stmt.default_block._repeat_stmt )]
				"}
		}
		case [case_label]
		{
			send Parser
				"case [expr( Stmt.case_label.expr )]:
		}
		case [AS: assign_stmt]
		{
			send Parser
				"[lvalue(AS.LValue) AS.assign_op expr(AS.expr)];
		}
		default {
			# catches unspecified cases
			send Parser [Stmt]
		}
	}

	void stmt_list( StmtList: stmt* )
	{
		for Stmt: stmt in repeat( StmtList )
			stmt( Stmt )
	}

	void trans( Output: stream, Start: start )
	{
		Parser = new parser<rust_out::rust_out>()

		if ( Start.opt_bom.bom )
			send Output [Start.opt_bom.bom]

		stmt_list( Start._repeat_stmt )

		CO: rust_out::rust_out = Parser->finish()

		if CO {
			send Output
				[CO]
		}
		else {
			send stderr
				"failed to parse output: [Parser->error]
		}
	}
end

void trans( Output: stream, Start: start )
{
	rust_gen::trans( Output, Start )
}

include 'rlhc-main.lm'
